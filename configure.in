dnl
dnl Process this file with autoconf to produce a configure script.

AC_PREREQ([2.65])

AC_INIT([inotify-java], [2.0.3], [https://bugs.launchpad.net/inotify-java])

AM_INIT_AUTOMAKE([dist-bzip2])
AM_MAINTAINER_MODE
AC_CONFIG_SRCDIR([src/cpp/inotify-interface.cpp])
AC_CONFIG_SRCDIR([src/java/classes/com/den_4/inotify_java/Inotify.java])
AC_CONFIG_HEADERS([config.h])

AC_PROG_CXX

AC_CHECK_HEADERS([unistd.h stdlib.h string.h])
AC_CHECK_HEADERS([sys/inotify.h sys/ioctl.h sys/errno.h])

AC_SUBST(JAVA_HOME)
if test "${JAVA_HOME}" = "" ; then
    AC_MSG_ERROR([no JAVA_HOME is set])
fi

AC_PATH_PROG(JAVA, java,false, $JAVA_HOME/bin)
if test "${JAVA}" = "false" ; then
    AC_MSG_ERROR([no java in \$JAVA_HOME/bin])
fi

AC_PATH_PROG(JAVAC,javac,false, $JAVA_HOME/bin)
if test "${JAVAC}" = "false" ; then
    AC_MSG_ERROR([no javac in \$JAVA_HOME/bin])
fi

AC_PATH_PROG(JAR,jar,false, $JAVA_HOME/bin)
if test "${JAR}" = "false" ; then
    AC_MSG_ERROR([no jar in \$JAVA_HOME/bin])
fi

AC_PATH_PROG(JAVAH,javah,false, $JAVA_HOME/bin)
if test "${JAVAH}" = "false" ; then
    AC_MSG_ERROR([no javah in \$JAVA_HOME/bin])
fi

AC_PATH_PROG(ANT,ant,false)
if test "${ANT}" = "false" ; then
    AC_MSG_ERROR([no ant in path])
fi

AC_TYPE_SIZE_T

AC_FUNC_FORK
AC_FUNC_MALLOC
AC_CHECK_FUNCS([pipe])

dnl
dnl other compiler options
dnl
AC_ARG_ENABLE([debug],
    [AS_HELP_STRING([--enable-debug],
        [use debug compiler flags and macros @<:@default=disabled@:>@])],
    [enable_debug="$enableval"],
    [enable_debug=no]
)
if test "x$enable_debug" = xyes; then
    DEFINES="-DENABLE_DEBUGGING"
    DEBUG="-g"
fi
AC_SUBST(DEFINES)
AC_SUBST(DEBUG)

AC_CONFIG_FILES([
                 Makefile
                 src/Makefile
                 src/cpp/Makefile
                 src/java/Makefile
                 inotify-java-2.0.pc
])

AC_OUTPUT
